(ns proserunner.vet
  "Computes results of running all the checks on each file,
   using cached results where possible."
  (:gen-class)
  (:require
   [proserunner
    [storage :as store]
    [result :as result]]
   [proserunner.vet
    [cache :as cache]
    [processor :as processor]
    [input :as input]]
   [editors
    [re :as re]
    [repetition :as repetition]
    [registry :as registry]
    [utilities :as util]]))

(set! *warn-on-reflection* true)

;;; Register all standard editors

(doseq [editor-type (util/standard-editor-types)]
  (registry/register-editor! editor-type (util/create-editor editor-type)))

(registry/register-editor! "repetition" repetition/proofread)
(registry/register-editor! "regex" re/proofread)

;;; Core computation

(defn compute
  "Takes an input, and returns the results of
  running the configured checks on each line of text in the file."
  [{:keys [file lines config checks output parallel-lines]}]
  (store/map->Result {:lines lines
                      :lines-hash (cache/stable-hash lines)
                      :file-hash (cache/stable-hash file)
                      :config config
                      :config-hash (cache/stable-hash config)
                      :check-hash (cache/stable-hash checks)
                      :output output
                      :results (processor/process checks lines parallel-lines)}))

(defn- compute-and-store
  [inputs]
  (let [result (compute inputs)]
    (store/save! result)
    result))

(defn compute-or-cached
  "Returns computed or cached results of running checks on text.

  Returns Result with computed/cached results, or Failure on error."
  [options]
  (let [input-result (input/make options)]
    (if (result/failure? input-result)
      input-result
      (let [inputs (:value input-result)
            {:keys [cached-result output]} inputs
            results
            (cond
              (:no-cache inputs)
              (compute-and-store inputs)

              (cache/valid-result? inputs)
              (assoc cached-result :output output)

              (cache/valid-checks? inputs)
              (let [result (cache/compute-changed inputs processor/process)]
                (store/save! result)
                result)

              :else
              (compute-and-store inputs))]
        (result/ok (assoc inputs :results results))))))
